# Maintainer: Roland Obermayer <roland@robelix.com>
# Contributor: PKGBUILD 192486 2013-08-13 05:59:14Z bpiotrowski $
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Sigmund Vestergaard <sigmund at gmail dot com>

pkgname=miro-git
pkgver=v6.0.r14.g9faacca86
pkgrel=1
pkgdesc="The free and open source internet TV platform"
arch=('any')
url="http://www.getmiro.com"
license=('GPL2')
depends=('python2-dbus' 'pyrex' 'pygtk' 'gstreamer0.10' 'python2-gconf'
         'python2-pysqlite' 'shared-mime-info'
         'desktop-file-utils' 'gstreamer0.10' 'hicolor-icon-theme'
         'gstreamer0.10-python' 'python2-notify' 'libtorrent-rasterbar'
         'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'python2-pycurl'
         'gstreamer0.10-ffmpeg' 'ffmpeg' 'mutagen' 'xdg-utils'
         'gst-python2'
         )
makedepends=('boost')
install=miro.install
source=("git+https://github.com/robelix/miro.git#branch=robelix")
sha256sums=('SKIP')

pkgver() {
  cd "miro"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/miro/tv/linux"

  python2 setup.py install --root="${pkgdir}"

  # fix miro startup script so --debug works with python2
  sed -i "s|which python|which python2|" "${pkgdir}/usr/bin/miro"
  sed -i "s|./miro.real|/usr/bin/miro.real|" "${pkgdir}/usr/bin/miro"

  # fix python scripts for python2
  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}"/usr/share/miro/resources/searchengines/update-icons.py
  for i in usr/share/miro/resources/testdata/echonest-replies/generate.py usr/share/miro/resources/searchengines/update-icons.py \
    usr/share/miro/resources/testdata/7digital-replies/generate.py; do
    sed -i "s|/usr/bin/python|/usr/bin/python2|" "${pkgdir}"/${i}
  done

  # Fakeroot segfaults on these; remove
  if [ ${CARCH} == "i686" ]; then
    rm -f "${pkgdir}"/usr/bin/codegen.Linux-x86_64 || true
  else
    rm -f "${pkgdir}"/usr/bin/codegen.Linux-i686 || true
  fi
}
